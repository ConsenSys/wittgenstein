plugins {
    id "com.diffplug.gradle.spotless" version "3.23.1"
    id "com.github.johnrengelman.shadow" version "4.0.4"
}

subprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.9
    targetCompatibility = 1.9

    apply plugin: 'maven'
    apply plugin: 'com.diffplug.gradle.spotless'

    repositories {
        mavenLocal()
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    dependencies {
        implementation "org.springframework.boot:spring-boot-starter-web:2.1.9.RELEASE"
        implementation 'com.google.guava:guava:28.1-jre'

        testImplementation 'junit:junit:4.12'
    }

    spotless {
        java {
            removeUnusedImports()
            googleJavaFormat('1.7')
            trimTrailingWhitespace()
            endWithNewline()
        }
    }

    test {
        // To use less memory during the tests, for the execution on circle ci...
        jvmArgs "-XX:+UseSerialGC"
        // We need to limit the memory to be on par with CircleCI
        maxHeapSize = "2400m"
        afterSuite { desc, result ->
            if (!desc.parent)
                println("${result.resultType} " +
                        "(${result.testCount} tests, " +
                        "${result.successfulTestCount} successes, " +
                        "${result.failedTestCount} failures, " +
                        "${result.skippedTestCount} skipped)")
        }
    }
}
